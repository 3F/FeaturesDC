@echo off
REM /*
REM  * Copyright (c) 2013 Developed by reg <entry.reg@gmail.com>
REM  * 
REM  * Distributed under the MIT license
REM  * (see accompanying file LICENSE or a copy at http://opensource.org/licenses/MIT)
REM */


REM generate help/cshelp.h with a standard command interpreter windows

REM range of 11000 - 11999
REM replacement help/gen_cshelp.py
REM detail on https://bitbucket.org/3F/featuresdc/issue/1/help-cshelph-dcpp-version-revnoinc

REM ##### variables #####

set range_begin=11000
set workdir=../../../src/help
set output=cshelp.h


REM ### processing #

setlocal enableDelayedExpansion
cd %workdir%

echo // generated by cshelp.bat > %output%
echo #ifndef DCPLUSPLUS_HELP_CSHELP_H >> %output%
echo #define DCPLUSPLUS_HELP_CSHELP_H >> %output%

    rem init
    set search_after=2
    set /a dnum=range_begin
    set /a pos=%search_after%
    set token_pos=!pos!
    
    for /F "tokens=2 delims=<" %%l in ('findstr /r "cshelp.*=.*\"[^\"]*" *.html') do (
        set line=%%l
        set /a pos=%search_after%
        set token_pos=!pos!
        rem echo !line!
        call :handler
    )

echo #endif >> %output%


REM ### finalize #

set /a count=!dnum!-!range_begin!
echo !count! done.
rem exit /b 0
exit 0


REM ### logics #

:handler
for /F delims^=^"^ tokens^=^%token_pos% %%d in ("!line!") do (
    set verify=%%d      
    if NOT "!verify:~0,4!" == "IDH_" (
        set /a pos+=1
        set token_pos=!pos!
        
        rem echo %token_pos% :: !verify:~0,4! === IDH_
        rem with changed offset:
        goto handler
    ) else (
        echo     #define %%d !dnum! >> %output%
        set /a dnum+=1
    )
)